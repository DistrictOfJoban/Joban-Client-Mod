subprojects {
	apply plugin: "java"

	group project.maven_group
	version "${rootProject.properties.mod_version}+${rootProject.properties.minecraft_version}"

	base {
		archivesName = "${rootProject.archives_base_name}-${project.properties.loader_name}"
	}

	dependencies {
		annotationProcessor 'systems.manifold:manifold-preprocessor:+'
		implementation 'com.google.code.findbugs:jsr305:+'
	}

	repositories {
		mavenCentral()
		flatDir { dirs "../libs" }
		maven { url "https://jitpack.io" }
	}

	def mc_ver = rootProject.properties.minecraft_version
	def mc_major = mc_ver.split("\\.")[0]
	def mc_main = mc_ver.split("\\.")[1]
	def mc_minor = mc_ver.split("\\.").size() > 2 ? (mc_ver.split("\\.")[2] as String).padLeft(2, '0') : 0
	def manifold_mc_version = "$mc_major$mc_main$mc_minor"

	ext { // Expose to subproject
		java_version = mc_main == 16 ? 8 : mc_main == 17 ? 16 : 17;
	}

	/* Manifold support */
	tasks.withType(JavaCompile).forEach {
		it.options.compilerArgs += [
				"-Xplugin:Manifold",
				"-AMC_VERSION=$manifold_mc_version",
				"-ALOADER=$project.properties.loader_name"
		]
	}

	// Some system don't compile with UTF-8 by default
	compileJava {
		options.encoding = 'UTF-8'
	}

	tasks.register('moveBuildFile', Copy) {
		from "${project.projectDir}/build/libs/${base.archivesName.get()}-${version}.jar"
		into "${rootProject.projectDir}/build"
	}
}