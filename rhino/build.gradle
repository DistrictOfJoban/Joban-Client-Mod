plugins {
    id 'java'
    id "de.undercouch.download" version "5.6.0"
}

def useLatestRhino = (rootProject.properties.minecraft_version as String).startsWith("1.16") ? false : true
def rhinoVersion = useLatestRhino ? "1_8_0" : "1_7_15"
def rhinoExist = project.file("src/main/java").exists()
def shadowPath = ["com", "lx862", "mtrscripting", "lib", "org", "mozilla"]

tasks.register('downloadRhino', Download) {
    src "https://github.com/mozilla/rhino/archive/refs/tags/Rhino${rhinoVersion}_Release.zip"
    dest "./src/main/rhino_src.zip"

    doLast {
        copy {
            from zipTree(dest)
            into "./src/main/"
        }
        delete(dest)
    }
}

tasks.register('setupRhino') {
    dependsOn downloadRhino
    def extractedSrc = project.file("src/main/rhino-Rhino${rhinoVersion}_Release")
    def srcDestination = project.file("src/main/java/${shadowPath.join("/")}")

    doFirst {
        srcDestination.mkdirs()

        copy {
            from extractedSrc.toPath().resolve(useLatestRhino ? "rhino/src/main/java/org/mozilla" : "src/org/mozilla/").toFile()
            into srcDestination
            filter {
                String line -> line.replaceAll('org/mozilla/', "${shadowPath.join("/")}/").replaceAll("org\\.mozilla", "${shadowPath.join(".")}")
            }
        }
    }

    doLast {
        extractedSrc.deleteDir()
    }
}

if (!rhinoExist) {
    compileJava.dependsOn(setupRhino)
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = useLatestRhino ? 16 : 8
    it.options.warnings = false // Mostly java.security stuff in Java 16+, not within our control
}

dependencies {
}